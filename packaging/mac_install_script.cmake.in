# file: mac_install_script.cmake
# author: Sho Ikeda
#
# Copyright (c) 2015-2017 Sho Ikeda
# This software is released under the MIT License.
# http://opensource.org/licenses/mit-license.php
# 

set(__package_script_dir__ ${CMAKE_CURRENT_LIST_DIR})
set(__project_root__ @PROJECT_SOURCE_DIR@)
set(__build_dir__ @PROJECT_BINARY_DIR@)
set(__project_name__ @PROJECT_NAME@)
set(__install_dir__ ${CMAKE_INSTALL_PREFIX})
set(__app_contents_path__ ${__install_dir__}/${__project_name__}.app/Contents)
set(__body_exec_name__ @body_exec_name@)


function(installFiles)
  # Deploy Qt libraries
  message("Deploying Qt libraries")
  execute_process(COMMAND macdeployqt ${__install_dir__}/${__project_name__}.app
                      -qmldir=${__project_root__}/source/NanairoGui)
  # Deploy other libraries
  message("Deploying other libraries")
  include(${__project_root__}/packaging/dependencies.cmake)
  set(library_dir ${__app_contents_path__}/Frameworks)
  file(MAKE_DIRECTORY ${library_dir})
  deployUnresolvedDependencies(${__app_contents_path__}/MacOS/${__project_name__}
                               ${library_dir}
                               ${__app_contents_path__}/Frameworks
                               ${__app_contents_path__}/Frameworks)

  # Deploy resources
  # Install icon
  set(icon_dir ${__app_contents_path__}/Resources)
  file(MAKE_DIRECTORY ${icon_dir})
  configure_file(${__project_root__}/packaging/icon.icns
                 ${icon_dir}/${__project_name__}.icns
                 COPYONLY)
  # Move exe file
  file(RENAME ${__app_contents_path__}/MacOS/${__project_name__}
              ${__app_contents_path__}/MacOS/${__body_exec_name__})
  # Install start script
  file(INSTALL ${__build_dir__}/packaging/${__project_name__}
       DESTINATION ${__app_contents_path__}/MacOS
       FILE_PERMISSIONS OWNER_READ OWNER_WRITE OWNER_EXECUTE
                        GROUP_READ GROUP_EXECUTE
                        WORLD_READ WORLD_EXECUTE)
endfunction(installFiles)

# Main
installFiles()
